if 1-?itoa_u16

;;; itoa_u16 - convert an unsigned 16-bit integer to decimal
;;; the output will consist of 1-5 decimal digits
;;;  in: HL: integer value
;;;      DE: address for the *last* character of output
;;; out: HL: zero
;;;      DE: address of the first digit written
;;; use: A, F, B, C
itoa_u16:

  ;; divide HL by 10 in-place

  ld bc, 0x1000   ; B = div loop count (16), C = numerator/remainder (0)

.div:

  ;; HL <<= 1, C <<= 1 + carry in

  sla l
  rl h
  rl c

  ;; if C >= 10: C -= 10, HL |= 1

  ld a, c
  sub 10
  jr c, .keep
  ld c, a
  ld a, 1
  or l
  ld l, a
.keep:

  djnz .div

  ;; HL' = HL/10, C = HL%10

  ld a, '0'
  add c
  ld (de), a

  ld a, l
  or h
  jr nz, .cont
  ret
.cont:

  dec de
  jr itoa_u16

endif
