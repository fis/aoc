  jp solve

include 'add_u32.z80'
include 'cp_u32.z80'
include 'read_u32.z80'
include 'write_u32.z80'

solve:
  ld ix, .temp
  call read_u32
  ld a, (ix+0)
  or (ix+1)
  or (ix+2)
  or (ix+3)
  jr nz, .start_group

  ld ix, .max_cal
  call write_u32
  ld a, '\n'
  out (1), a
  halt

.start_group:
  ld hl, .temp
  ld de, .cur_cal
  ld bc, 4
  ldir
.loop:
  ld ix, .temp
  call read_u32
  ld a, (ix+0)
  or (ix+1)
  or (ix+2)
  or (ix+3)
  jr z, .end_group
  ld ix, .cur_cal
  ld iy, .temp
  call add_u32
  jr .loop

.end_group:
  ld ix, .max_cal
  ld iy, .cur_cal
  call cp_u32
  jr nc, solve
  ld hl, .cur_cal
  ld de, .max_cal
  ld bc, 4
  ldir
  jr solve

.temp: defs 4
.cur_cal: defs 4
.max_cal: defs 4
